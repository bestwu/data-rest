buildscript {
    repositories {
        try {
            maven { url mavenLocalUrl }
        } catch (ignored) {
            mavenLocal()
        }
        jcenter()
        maven { url 'http://repo.spring.io/plugins-release/' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.6'
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE'
    }
}

subprojects {

    group 'cn.bestwu'
    version '1.0.0-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'propdeps'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    [compileJava, javadoc, compileTestJava]*.options*.encoding = 'UTF-8'
    [compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:none', '-Xlint:unchecked']

    repositories {
        try {
            maven { url mavenLocalUrl }
        } catch (ignored) {
            mavenLocal()
        }
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
        }
        dependencies {
            dependency 'com.mysema.querydsl:querydsl-apt:3.7.1'
            dependency 'com.mysema.querydsl:querydsl-jpa:3.7.1'
            dependency 'com.mysema.querydsl:querydsl-mongodb:3.7.1'
            dependency 'org.hibernate:hibernate-search-orm:5.3.0.Final'
            dependency 'org.apache.lucene:lucene-highlighter:4.10.4'

            dependency 'org.atteo:evo-inflector:1.2.1'
            dependency 'commons-io:commons-io:2.4'
            dependency 'com.belerweb:pinyin4j:2.5.0'
        }
    }
}

apply plugin: 'idea'
idea {
    project {
        jdkName = '1.8'
        languageLevel = '1.8'
    }
}

wrapper {
    gradleVersion = '2.11'
}